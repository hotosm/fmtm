# Copyright (c) 2024 Humanitarian OpenStreetMap Team
#
# This file is part of FMTM.
#
#     FMTM is free software: you can redistribute it and/or modify
#     it under the terms of the GNU General Public License as published by
#     the Free Software Foundation, either version 3 of the License, or
#     (at your option) any later version.
#
#     FMTM is distributed in the hope that it will be useful,
#     but WITHOUT ANY WARRANTY; without even the implied warranty of
#     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with FMTM.  If not, see <https:#www.gnu.org/licenses/>.
#

# Start FMTM
[no-cd]
default:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}
  docker compose up -d

# Start backend API only
[no-cd]
backend:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}
  docker compose up -d api

# Start backend API only
[no-cd]
backend-no-docker:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}/src/backend

  FMTM_DOMAIN="" OSM_CLIENT_ID="" OSM_CLIENT_SECRET="" \
  OSM_SECRET_KEY="" ENCRYPTION_KEY="" \
    pdm run uvicorn app.main:api --host 0.0.0.0 --port 8000

# Start frontend UI only
[no-cd]
frontend:
  docker compose up -d ui

# Start FMTM without ODK Central
[no-cd]
without-central:
  docker compose --profile no-odk up -d

# Start FMTM with JOSM
[no-cd]
josm:
  docker compose \
    -f docker-compose.yml \
    -f contrib/josm/docker-compose.yml \
    up -d
  
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
  @echo " Access the JOSM Remote API: http://localhost:8111"
  @echo " Access the JOSM GUI in browser: http://localhost:8112"
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo

# Start FMTM with tunnels
[no-cd]
tunnel:
  #!/usr/bin/env sh

  docker compose \
    -f docker-compose.yml \
    -f contrib/tunnel/docker-compose.yml \
    up --wait

  # Workaround to until PR merged:
  # https://github.com/cloudflare/cloudflared/pull/1135
  # Wait until services ready without HEALTHCHECK
  sleep 5

  fmtm_url=$(just --unstable start _get-tunnel-url 'frontend')
  api_url=$(just --unstable start _get-tunnel-url 'api')
  odk_url=$(just --unstable start _get-tunnel-url 'central')
  s3_url=$(just --unstable start _get-tunnel-url 's3')

  just --unstable dotenv update "EXTRA_CORS_ORIGINS" "${fmtm_url}"
  just --unstable dotenv update "S3_ENDPOINT" "${s3_url}"

  # Restart the containers with env vars set
  # (API url for frontend, domain for Central form download urls)
  CENTRAL_DOMAIN_OVERRIDE="$(echo "${odk_url}" | sed 's|^https://||')" \
  API_URL="$(echo "${api_url}" | sed 's|^https://||')" \
  docker compose \
    -f docker-compose.yml \
    -f contrib/tunnel/docker-compose.yml \
    up -d api ui central

  just --unstable start _print-tunnel-urls "$fmtm_url" "$api_url" "$odk_url" "$s3_url"

# View the URLs for created tunnels
[no-cd]
view-tunnel-urls:
  #!/usr/bin/env sh

  fmtm_url=$(just --unstable start _get-tunnel-url 'frontend')
  api_url=$(just --unstable start _get-tunnel-url 'api')
  odk_url=$(just --unstable start _get-tunnel-url 'central')
  s3_url=$(just --unstable start _get-tunnel-url 's3')

  just --unstable start _print-tunnel-urls "$fmtm_url" "$api_url" "$odk_url" "$s3_url"

[no-cd]
_get-tunnel-url service_name:
  #!/usr/bin/env sh

  service_url=$(docker compose \
    -f docker-compose.yml \
    -f contrib/tunnel/docker-compose.yml \
    logs {{service_name}}-tunnel | \
    grep 'Your quick Tunnel' -A 1 | tail -n 1 | \
    sed -n 's/.*| *\(https:\/\/[^ ]*\).*/\1/p')

  echo "$service_url"

[no-cd]
_print-tunnel-urls fmtm_url api_url odk_url s3_url:
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
  @echo "\033[0;34m FMTM URL: \033[0m"
  @echo " {{fmtm_url}}"
  @echo
  @echo "\033[0;34m API URL: \033[0m"
  @echo " {{api_url}}"
  @echo
  @echo "\033[0;34m ODK Central URL: \033[0m"
  @echo " {{odk_url}}"
  @echo
  @echo "\033[0;34m S3 URL: \033[0m"
  @echo " {{s3_url}}"
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo



