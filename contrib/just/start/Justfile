# Copyright (c) Humanitarian OpenStreetMap Team
#
# This file is part of Field-TM.
#
#     Field-TM is free software: you can redistribute it and/or modify
#     it under the terms of the GNU General Public License as published by
#     the Free Software Foundation, either version 3 of the License, or
#     (at your option) any later version.
#
#     Field-TM is distributed in the hope that it will be useful,
#     but WITHOUT ANY WARRANTY; without even the implied warranty of
#     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with Field-TM.  If not, see <https:#www.gnu.org/licenses/>.
#

# Start Field-TM
[no-cd]
default:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}
  docker compose up -d

# Production deploy, depending which branch on current
[no-cd]
prod:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}
  export GIT_BRANCH=$(git rev-parse --abbrev-ref HEAD)
  bash deploy/gen-and-deploy.sh

# Start backend API only
[no-cd]
backend:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}
  docker compose up -d api

# Start backend API without docker
[no-cd]
backend-no-docker:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}/src/backend

  FMTM_DOMAIN="" OSM_CLIENT_ID="" OSM_CLIENT_SECRET="" \
  OSM_SECRET_KEY="" ENCRYPTION_KEY="" \
    uv run uvicorn app.main:api --host 0.0.0.0 --port 8000

# Start frontend UI (also starts backend)
[no-cd]
frontend:
  docker compose up -d ui

# Start frontend UI without docker, connected to staging
[no-cd]
frontend-dev:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}/src/frontend

  pnpm install
  VITE_API_URL=https://api.stage.fmtm.hotosm.org \
  pnpm run dev

# Start mapper frontend UI without docker, connected to staging
[no-cd]
mapper-frontend-dev:
  #!/usr/bin/env sh

  cd {{justfile_directory()}}/src/mapper

  pnpm install
  VITE_API_URL=https://api.stage.fmtm.hotosm.org \
  VITE_SYNC_URL=https://sync.stage.fmtm.hotosm.org \
  VITE_S3_URL=https://s3.stage.fmtm.hotosm.org \
  pnpm run dev

# Start Field-TM without ODK Central
[no-cd]
without-central:
  docker compose --profile no-odk up -d

# Start Field-TM with JOSM
[no-cd]
josm:
  docker compose \
    -f compose.yaml \
    -f contrib/josm/compose.yaml \
    up -d
  
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
  @echo " Access the S3 Browser at: http://localhost:8111"
  @echo " Access the JOSM GUI in browser: http://localhost:8112"
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo

# Start S3 browser
[no-cd]
s3-browser:
  docker compose \
    -f contrib/s3_browser/compose.yaml \
    up -d
  
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
  @echo " Access the S3 Browser at:    http://localhost:8334"
  @echo ""
  @echo " Access Minio S3 buckets at:  http://localhost:9000"
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo


# Externally accessible ODK Central for tests
[no-cd]
tunnel:
  #!/usr/bin/env sh

  docker compose \
    -f compose.yaml \
    -f contrib/tunnel/compose.yaml \
    up --wait

  # Workaround to until PR merged:
  # https://github.com/cloudflare/cloudflared/pull/1135
  # Wait until services ready without HEALTHCHECK
  sleep 5

  odk_url=$(just start _get-tunnel-url 'central')
  button_url=$(just start _get-tunnel-url 'button')

  # Restart Central with DOMAIN var set to new tunnel URL.
  # Is is required to correctly download forms from Collect
  CENTRAL_DOMAIN_OVERRIDE="$(echo "${odk_url}" | sed 's|^https://||')" \
  docker compose \
    -f compose.yaml \
    -f contrib/tunnel/compose.yaml \
    up -d central

  just start _print-tunnel-url "$odk_url" "$button_url"

# View the URLs for created tunnels
[no-cd]
view-tunnel-url:
  #!/usr/bin/env sh

  odk_url=$(just start _get-tunnel-url 'central')
  button_url=$(just start _get-tunnel-url 'button')
  just start _print-tunnel-url "$odk_url" "$button_url"

[no-cd]
_get-tunnel-url service_name:
  #!/usr/bin/env sh

  service_url=$(docker compose \
    -f compose.yaml \
    -f contrib/tunnel/compose.yaml \
    logs {{service_name}}-tunnel | \
    grep 'Your quick Tunnel' -A 1 | tail -n 1 | \
    sed -n 's/.*| *\(https:\/\/[^ ]*\).*/\1/p')

  echo "$service_url"

[no-cd]
_print-tunnel-url odk_url button_url:
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
  @echo "\033[0;34m ODK Central URL: \033[0m"
  @echo " {{odk_url}}"
  @echo
  @echo "\033[0;34m Frontend Button URL: \033[0m"
  @echo " {{button_url}}"
  @echo
  @echo "\033[0;33m ############################################### \033[0m"
  @echo
