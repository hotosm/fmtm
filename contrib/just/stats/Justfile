# Copyright (c) Humanitarian OpenStreetMap Team
#
# This file is part of Field-TM.
#
#     Field-TM is free software: you can redistribute it and/or modify
#     it under the terms of the GNU General Public License as published by
#     the Free Software Foundation, either version 3 of the License, or
#     (at your option) any later version.
#
#     Field-TM is distributed in the hope that it will be useful,
#     but WITHOUT ANY WARRANTY; without even the implied warranty of
#     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#     GNU General Public License for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with Field-TM.  If not, see <https:#www.gnu.org/licenses/>.
#

[no-cd]
_run_stats_script script_name:
  #!/usr/bin/env sh
  cd {{justfile_directory()}}

  # Note in Justfile syntax, using {{{{}} works as escape char for double curly braces
  network=$(docker network ls --format '{{{{.Name}}' | grep '^fmtm-' | head -n 1)
  if [ -z "$network" ]; then echo "No fmtm docker network found!" >&2; exit 1; fi
  echo "Using network: $network"

  # This works by running a debug API attached to the same network as prod containers
  # it accesses the prod db and Central instance
  # NOTE there must be a .env file present with the correct env vars
  docker run --rm -d --name fmtm-stats \
    --network "$network" \
    -e DEBUG=True --env-file=./.env \
    ghcr.io/hotosm/field-tm/backend:debug

  docker exec fmtm-stats python /opt/stats/{{script_name}}

  docker rm --force fmtm-stats

# Timeseries data about projects
[no-cd]
timeseries:
  just stats _run_stats_script timeseries.py

# Get total feature mapped per project
[no-cd]
total-mapped:
  just stats _run_stats_script total_mapped.py
